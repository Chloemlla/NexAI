plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '106'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.1.6'
}

// Read version metadata from nexai_release.json (generated by build.ps1)
def nexaiName = flutterVersionName
def nexaiCode = flutterVersionCode
def nexaiShort = 'unknown'

def nexaiFile = rootProject.file('../nexai_release.json')
if (nexaiFile.exists()) {
    def nexaiJson = new groovy.json.JsonSlurper().parseText(nexaiFile.text)
    nexaiName = nexaiJson['nexai.name'] ?: nexaiName
    nexaiCode = "${nexaiJson['nexai.code'] ?: nexaiCode}"
    nexaiShort = nexaiJson['nexai.short'] ?: nexaiShort
    flutterVersionCode = nexaiCode
    flutterVersionName = "${nexaiName}-${nexaiShort}"
} else {
    // Fallback: append git short SHA
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        nexaiShort = stdout.toString().trim()
        flutterVersionName = "${flutterVersionName}-${nexaiShort}"
    } catch (Exception e) {
        logger.warn("Failed to get git SHA, using 'unknown'")
        flutterVersionName = "${flutterVersionName}-${nexaiShort}"
    }
}

android {
    namespace "com.chloemlla.nexai"
    compileSdk 35

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    kotlinOptions {
        jvmTarget = "21"
    }

    defaultConfig {
        applicationId "com.chloemlla.nexai"
        minSdk 24
        targetSdk 35
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        multiDexEnabled true
    }

    // Signing config for release builds (CI injects via gradle.properties)
    def nexaiStoreFile = project.hasProperty('NEXAI_STORE_FILE') ? project.property('NEXAI_STORE_FILE') : null
    if (nexaiStoreFile != null) {
        signingConfigs {
            release {
                storeFile file(nexaiStoreFile)
                storePassword project.property('NEXAI_STORE_PASSWORD')
                keyAlias project.property('NEXAI_KEY_ALIAS')
                keyPassword project.property('NEXAI_KEY_PASSWORD')
            }
        }
    }

    buildTypes {
        release {
            signingConfig nexaiStoreFile != null ? signingConfigs.release : signingConfigs.debug
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            ndk {
                debugSymbolLevel 'SYMBOL_TABLE'
            }
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }

    // 16KB page size alignment for Android 15+ / Google Play requirement
    packaging {
        jniLibs {
            useLegacyPackaging false
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "androidx.core:core-ktx:1.15.0"
    implementation "androidx.appcompat:appcompat:1.7.0"

    // Force transitive dependencies to versions compatible with compileSdk 35 / AGP 8.7.3
    constraints {
        implementation("androidx.core:core:1.15.0") {
            because "1.17.0 requires compileSdk 36"
        }
        implementation("androidx.core:core-ktx:1.15.0") {
            because "1.17.0 requires compileSdk 36"
        }
        implementation("androidx.browser:browser:1.8.0") {
            because "1.9.0 requires compileSdk 36 and AGP 8.9.1+"
        }
    }
}
