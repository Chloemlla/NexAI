buildscript {
    repositories {
        google()
        mavenCentral()
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}

// Force all subprojects (Flutter plugins) to use compileSdk 36
subprojects { subproject ->
    subproject.afterEvaluate {
        if (subproject.hasProperty('android')) {
            subproject.android {
                if (namespace == null) {
                    namespace subproject.group ?: subproject.name
                }
                compileSdk 36
            }
        }
    }
}

// Polyfill jcenter() â†’ mavenCentral() for plugins still referencing it (e.g. system_theme 3.2.0).
// jcenter() method was removed in Gradle 9.
// We patch DefaultRepositoryHandler's metaClass at the class level so it applies everywhere.
org.gradle.api.internal.artifacts.dsl.DefaultRepositoryHandler.metaClass.jcenter = { ->
    delegate.mavenCentral()
}
